<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="25">
<Class name="MDX2JSON.DashboardRS">
<Description>
Custom result set to get dashboard list, visible to user in a specific folder</Description>
<Super>%SQL.CustomResultSet</Super>
<TimeCreated>63943,47866.588743</TimeCreated>

<Parameter name="global">
<Default>^DeepSee.FolderItemD</Default>
</Parameter>

<Parameter name="dashclass">
<Default>%DeepSee.Dashboard.Definition</Default>
</Parameter>

<Property name="title">
<Type>%String</Type>
</Property>

<Property name="path">
<Type>%String</Type>
</Property>

<Property name="cover">
<Type>%String</Type>
</Property>

<Property name="id">
<Type>%String</Type>
<Private>1</Private>
</Property>

<Property name="folder">
<Type>%String</Type>
<Private>1</Private>
</Property>

<Method name="%OpenCursor">
<FormalSpec>folderName:%String=""</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	return:((folderName'="") && ('$IsObject(##class(%DeepSee.UserLibrary.Utils).%FindFolder(folderName)))) $$$ERROR($$$GeneralError,"Folder " _ folderName _ " does not exist")
    set ..id = ""
    set ..folder = folderName
    return $$$OK
]]></Implementation>
</Method>

<Method name="%Next">
<FormalSpec><![CDATA[&sc:%Library.Status]]></FormalSpec>
<PlaceAfter>%Execute</PlaceAfter>
<ReturnType>%Integer</ReturnType>
<Implementation><![CDATA[
    set sc = $$$OK
    do ..SetNextId()
    set id = ..id
    return:id="" $$$NO
    
    set ..title = ##class(%DeepSee.UserPortal.Utils).%ResolveText(##class(%DeepSee.UserLibrary.FolderItem).titleGetStored(id))
    set ..path =  ..GetDashFullName(id)
    set ..cover = ##class(MDX2JSON.Dashboard).GetDashCover(##class(%DeepSee.UserLibrary.FolderItem).bookCoverGetStored(id))
    
    return $$$YES
]]></Implementation>
</Method>

<Method name="SetNextId">
<Description>
Iterate over data global till we find dashboard definition
thet satisfies all conditions</Description>
<Implementation><![CDATA[
	set ..id = $order(@..#global@(..id))
    if ((..id'="") && ('..CheckConditions(..id))) {
	    // we are not at the last row and we failed condition check
	    // so we get the next id
	    do ..SetNextId()	
    }
]]></Implementation>
</Method>

<Method name="CheckConditions">
<Description>
Check if element with that id satisfies our conditions</Description>
<FormalSpec>id</FormalSpec>
<ReturnType>%Boolean</ReturnType>
<Implementation><![CDATA[
	/// first we check that it's a dashboard (not a pivot, etc.)
	set oid = ##class(%DeepSee.UserLibrary.FolderItem).%ComposeOid(id)
    do ##class(%DeepSee.UserLibrary.FolderItem).%OnDetermineClass(oid, .class)
    return:class'=..#dashclass $$$NO
    
    /// second that it's not a hidden or deleted dashboard
    set folder = ##class(%DeepSee.UserLibrary.FolderItem).folderGetStored(id)
    set folderpath = ##class(%DeepSee.UserLibrary.Folder).%GetFullName(folder)
    return:(($extract(folderpath,1,6)="$TRASH") || ($extract(folderpath,1,6)="$TRASH")) $$$NO

    /// third we check if a dashboard is in a correct folder
    return:($extract(folderpath,1,$length(..folder))'=..folder) $$$NO
    
    /// fourth we check if the user has access to a dashboard
	return:'..CheckItemResource(id) $$$NO
	
	return $$$YES
]]></Implementation>
</Method>

<Method name="GetDashFullName">
<Description>
Get dashboard full name by id</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>id:%String</FormalSpec>
<Implementation><![CDATA[
	set folder = ##class(%DeepSee.UserLibrary.FolderItem).folderGetStored(id)
    set name = ##class(%DeepSee.UserLibrary.FolderItem).nameGetStored(id)
    return ##class(%DeepSee.Dashboard.Definition).fullNameCompute(id, folder, name)
]]></Implementation>
</Method>

<Method name="CheckItemResource">
<Description>
Test if the current user has access to this item.</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>id:%String,privilege:%String="USE"</FormalSpec>
<ReturnType>%Boolean</ReturnType>
<Implementation><![CDATA[
	set access = $$$NO
	set resource = ..GetItemResource(id)
	if ((resource="")||($System.Security.Check(resource, privilege))) {
		set access = $$$YES
	}
	return access
]]></Implementation>
</Method>

<Method name="GetItemResource">
<Description>
Get the resource value for this item.</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>id:%String</FormalSpec>
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[
	set resource = ##class(%DeepSee.UserLibrary.FolderItem).resourceGetStored(id)
	if (resource="") {
		// inherit from folder
		if ##class(%DeepSee.UserLibrary.FolderItem).folderGetStored(id)'="" {
			set resource = ..GetFolderResource(##class(%DeepSee.UserLibrary.FolderItem).folderGetStored(id))
		}
	}
	return resource
]]></Implementation>
</Method>

<Method name="GetFolderResource">
<Description>
Get the resource value for this folder.</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>folderId:%String</FormalSpec>
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[
	if (##class(%DeepSee.UserLibrary.Folder).resourceGetStored(folderId)'="") {
		return ##class(%DeepSee.UserLibrary.Folder).resourceGetStored(folderId)
	} elseif (##class(%DeepSee.UserLibrary.Folder).folderGetStored(folderId)'="") {
		return ..GetFolderResource(##class(%DeepSee.UserLibrary.Folder).folderGetStored(folderId))
	}
	return ""
]]></Implementation>
</Method>

<Method name="Test">
<Description>
Get all dashes from SAMPLES namespace
do ##class(MDX2JSON.DashboardRS).Test()</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>folderName:%String=""</FormalSpec>
<Implementation><![CDATA[
	new $namespace
	set $namespace = "Samples"
	set rs = ..%New(,folderName)
	do rs.%Display()
]]></Implementation>
</Method>

<Method name="WriteJSON">
<Description>
 Execute query and output results as JSON</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>folderName:%String=""</FormalSpec>
<Implementation><![CDATA[
	#define CRLF 							$c(13,10)
	#define ZENJSISNUM(%val)				($IsValidNum(%val)&&(%val=+(%val)))
	#define ZENJSNUM(%num)					$fnumber(%num,"LON")
	#; JSON utility macros that use the JSON translation table instead of the JS translation table
	#define ZENJSTABLE(%format)				("JS"_$S(%format["u":"ML",1:""))
	#define ZENJSONTABLE(%format)			("JSON"_$S((%format["u"):"ML",1:""))
	#define ZENJSONESCAPE(%str,%format)		$S(%format["s":$ZCVT(%str,"O",$$$ZENJSONTABLE(%format)),1:$Replace($ZCVT(%str,"O",$$$ZENJSTABLE(%format)),"\'","'"))
	#define ZENJSONSTR(%str,%format)		(""""_$$$ZENJSONESCAPE(%str,%format)_"""")
	#define ZENJSONPROP(%prop,%format)		$$$ZENJSONSTR(%prop,%format)
	#define ZENJSONVALUE(%val,%format)		$S($$$ZENJSISNUM(%val):$$$ZENJSNUM(%val),$C(0)=(%val)||$ListValid(%val):"""""",1:$$$ZENJSONSTR(%val,%format))
	#define ZENJSONPAIR(%pr,%val,%format)	$$$ZENJSONPROP(%pr,%format)_":"_$$$ZENJSONVALUE(%val,%format)
	
	set rs = ..%New(,folderName)
	write "{ ""children"":["
	set separator=0
	while rs.%Next() {
		write:separator ",",$$$CRLF
		write "{"
        write $$$ZENJSONPAIR("title",rs.title,""),","
        write $$$ZENJSONPAIR("path",rs.path,""),","
        write $$$ZENJSONPAIR("Cover",rs.cover,"")
        write "}"
        
        set separator = 1
    }
    write "]}"
]]></Implementation>
</Method>
</Class>
</Export>
